buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.flywaydb:flyway-gradle-plugin:4.1.2'
    }
}

// use gradlew -Penv=${env} to pass
def environment = hasProperty('env') ? env : null

apply plugin: org.flywaydb.gradle.FlywayPlugin

dependencies {
    runtime 'mysql:mysql-connector-java:6.0.6'
}

flyway {
    placeholderReplacement = false

    def flywayProperties = "src/main/resources/flyway.properties"
    if (environment != null) {
        def envResources = "conf/${environment}/resources"
        assert project.file(envResources).exists()
        sourceSets.main.resources.srcDirs envResources
        flywayProperties = "${envResources}/flyway.properties"
    }
    assert project.file(flywayProperties).exists()

    Properties properties = new Properties()
    project.file(flywayProperties).withInputStream {
        properties.load(it)
    }
    properties.each { key, value ->
        project.ext[key] = value
    }
}
