apply from: file("${rootDir}/gradle/project.gradle")

subprojects {
    apply from: file("${rootDir}/gradle/quality.gradle")

    group = 'core.framework'
    version = '3.2.7'
}

def awsVersion = '1.10.2'
def jacksonVersion = '2.5.4'

project(':core-ng-api') {
    apply from: file("${rootDir}/gradle/lib.gradle")
}

project(':core-ng') {
    apply from: file("${rootDir}/gradle/lib.gradle")
    dependencies {
        provided(
                'redis.clients:jedis:2.7.2',
                'org.elasticsearch:elasticsearch:1.7.0',
                'com.mchange:c3p0:0.9.5',
                'org.mongodb:mongo-java-driver:3.0.1',
                'com.rabbitmq:amqp-client:3.5.3',
                "com.amazonaws:aws-java-sdk-core:${awsVersion}@jar",
                "com.amazonaws:aws-java-sdk-sqs:${awsVersion}@jar",
                "com.amazonaws:aws-java-sdk-sns:${awsVersion}@jar"
        )
        compile(
                project(":core-ng-api"),
                'javax.inject:javax.inject:1',
                'org.javassist:javassist:3.20.0-GA',
                'org.slf4j:slf4j-api:1.7.12',
                "com.fasterxml.jackson.module:jackson-module-jaxb-annotations:${jacksonVersion}",
                "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonVersion}",
                "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${jacksonVersion}",
                'org.apache.httpcomponents:httpclient:4.4.1',
                'io.undertow:undertow-core:1.2.8.Final'
        )
        testCompile(
                'junit:junit:4.12',
                'org.mockito:mockito-core:1.10.19',
                'org.hsqldb:hsqldb:2.3.2'
        )
    }
}

project(':core-ng-test') {
    apply from: file("${rootDir}/gradle/lib.gradle")
    dependencies {
        compile(
                project(":core-ng"),
                'junit:junit:4.12',
                'org.mockito:mockito-core:1.10.19',
                'org.hsqldb:hsqldb:2.3.2'
        )
    }
}

configure(subprojects.findAll { it.name.startsWith('core-ng') }) {
    apply plugin: 'maven-publish'

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                artifact sourcesJar {
                    classifier 'sources'
                }
            }
        }

        repositories {
            maven {
                // put following values in gradle.properties, e.g. systemProp.core-ng.repo.s3.url = s3://xxx
                url System.properties['core-ng.repo.s3.url']
                credentials(AwsCredentials) {
                    accessKey System.properties['core-ng.repo.s3.accessKey']
                    secretKey System.properties['core-ng.repo.s3.secretKey']
                }
            }
        }
    }
}

project(':demo-service') {
    apply from: file("${rootDir}/gradle/app.gradle")

    dependencies {
        compile(
                project(':core-ng'),
                'org.mongodb:mongo-java-driver:3.0.1'
        )
        runtime(
                'com.mchange:c3p0:0.9.5',
                'com.rabbitmq:amqp-client:3.5.3',
                'redis.clients:jedis:2.7.2'
        )
        testCompile(
                project(':core-ng-test')
        )
    }
}

project(':search-service') {
    apply from: file("${rootDir}/gradle/app.gradle")

    dependencies {
        compile(
                project(':core-ng'),
                'org.elasticsearch:elasticsearch:1.7.0'
        )
        testCompile(
                project(':core-ng-test')
        )
    }
}