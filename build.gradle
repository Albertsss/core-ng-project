apply from: file("${rootDir}/gradle/project.gradle")

subprojects {
    apply from: file("${rootDir}/gradle/quality.gradle")

    group = 'core.framework'
    version = '4.8.1'
}

def mongoVersion = '3.3.0'
def elasticVersion = '2.4.0'
def rabbitVersion = '3.6.5'
def kafkaVersion = '0.10.1.0'

project(':core-ng-api') {
    apply from: file("${rootDir}/gradle/lib.gradle")
}

project(':core-ng') {
    apply from: file("${rootDir}/gradle/lib.gradle")
    dependencies {
        provided(
                'redis.clients:jedis:2.8.1@jar',
                "org.elasticsearch:elasticsearch:${elasticVersion}@jar",
                'com.carrotsearch:hppc:0.7.1@jar',  // used by elasticsearch
                "org.mongodb:mongo-java-driver:${mongoVersion}",
                "com.rabbitmq:amqp-client:${rabbitVersion}",
                "org.apache.kafka:kafka-clients:${kafkaVersion}@jar"
        )
        compile(
                project(":core-ng-api"),
                'javax.inject:javax.inject:1',
                'org.javassist:javassist:3.21.0-GA',
                'org.slf4j:slf4j-api:1.7.21',
                'com.fasterxml.jackson.module:jackson-module-jaxb-annotations:2.8.2',
                'com.fasterxml.jackson.module:jackson-module-afterburner:2.8.2',
                'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.8.2',
                'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.8.2',
                'org.apache.httpcomponents:httpclient:4.5.2',
                'io.undertow:undertow-core:1.4.3.Final'
        )
        testCompile(
                'junit:junit:4.12',
                'org.mockito:mockito-core:2.1.0',
                'org.hsqldb:hsqldb:2.3.3'
        )
    }
}

project(':core-ng-test') {
    apply from: file("${rootDir}/gradle/lib.gradle")
    dependencies {
        provided(
                "org.mongodb:mongo-java-driver:${mongoVersion}",
                'com.github.fakemongo:fongo:2.0.7',
                "com.rabbitmq:amqp-client:${rabbitVersion}",
                'org.hsqldb:hsqldb:2.3.3',
                "org.elasticsearch:elasticsearch:${elasticVersion}"
        )
        compile(
                project(":core-ng"),
                'junit:junit:4.12',
                'org.mockito:mockito-core:2.1.0'
        )
    }
}

configure(subprojects.findAll { it.name.startsWith('core-ng') }) {
    apply plugin: 'maven-publish'

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                artifact sourcesJar {
                    classifier 'sources'
                }
            }
        }

        repositories {
            maven {
                url System.properties['maven.repo']     // set in gradle.properties, e.g. systemProp.maven.repo=/path
            }
        }
    }
}

project(':log-processor') {
    apply from: file("${rootDir}/gradle/app.gradle")

    dependencies {
        compile(
                project(':core-ng'),
                "org.elasticsearch:elasticsearch:${elasticVersion}",
                "org.apache.kafka:kafka-clients:${kafkaVersion}"
        )
        testCompile(
                project(':core-ng-test')
        )
    }
}

project(':benchmark') {
    dependencies {
        compile(
                project(':core-ng'),
                'org.openjdk.jmh:jmh-generator-annprocess:1.11.3'
        )
    }
}

project(':demo-service') {
    apply from: file("${rootDir}/gradle/app.gradle")

    dependencies {
        compile(
                project(':core-ng'),
                'redis.clients:jedis:2.8.1@jar',
                "org.mongodb:mongo-java-driver:${mongoVersion}",
                "org.apache.kafka:kafka-clients:${kafkaVersion}"
        )
        runtime(
                "com.rabbitmq:amqp-client:${rabbitVersion}"
        )
        testCompile(
                project(':core-ng-test')
        )
    }
}

project(':demo-site') {
    apply from: file("${rootDir}/gradle/app.gradle")

    dependencies {
        compile(
                project(':core-ng')
        )
        runtime(
                "com.rabbitmq:amqp-client:${rabbitVersion}"
        )
        testCompile(
                project(':core-ng-test')
        )
    }
}